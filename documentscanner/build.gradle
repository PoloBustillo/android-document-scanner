plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'signing'
}

android {
    compileSdk 31

    defaultConfig {
        minSdk 21
        targetSdk 31

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.websitebeaver:opencv:4.1.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = "io.github.polobustillo"
            artifactId = "documentscanner"
            version = "1.4.5-alpha"

            afterEvaluate {
                from components.release

                pom {
                    name = 'Document Scanner'
                    description = 'Document Scanner with opencv, can take multiple documents'
                    url = 'https://github.com/PoloBustillo/android-document-scanner'
                    licenses {
                        license {
                            name = 'The MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'dmarcs'
                            name = 'David Marcus'
                            email = 'dmarcus@websitebeaver.com'
                        }
                        developer {
                            id = 'polobustillo'
                            name = 'Mario Bustillo'
                            email = 'leopoldobeguiluz1@hotmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git@github.com:PoloBustillo/android-document-scanner.git'
                        developerConnection = 'scm:git@github.com:PoloBustillo/android-document-scanner.git'
                        url = 'https://github.com/PoloBustillo/android-document-scanner'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "mavencentral"
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl


        }
    }
}

signing {

    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.release
}